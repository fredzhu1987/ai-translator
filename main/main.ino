#include <U8g2lib.h>
#include <Wire.h>
#include <driver/i2s.h>
#include <WiFi.h>
#include <WebServer.h>
#include <SPIFFS.h>
#include <ArduinoJson.h>
#include <mbedtls/md.h>
#include <mbedtls/base64.h>
#include <mbedtls/sha256.h>
#include <ArduinoWebsockets.h>  //https://github.com/gilmaimon/ArduinoWebsockets
#include <NTPClient.h>
#include <Base64_Arturo.h>

using namespace websockets;


// éº¦å…‹é£ INMP441 (I2S è¾“å…¥)
#define I2S_MIC_SCK 15
#define I2S_MIC_WS 16
#define I2S_MIC_SD 17
#define SAMPLE_RATE 16000
#define CHANNELS 1
#define BIT_DEPTH 16
#define FRAME_SIZE 1280  // 16-bit PCMï¼Œæ¯å¸§ 1280B å¯¹åº” 40ms
#define CHUNK_SIZE 1024


// æ‰¬å£°å™¨
#define I2S_SPK_LRC 11
#define I2S_SPK_BCLK 12
#define I2S_SPK_DIN 13


// oled
#define OLED_SCL 5
#define OLED_SDA 4


// æŒ‰é’®
#define BUTTON_PIN_1 18
#define BUTTON_PIN_2 8
bool buttonLastState1 = HIGH;
bool buttonLastState2 = HIGH;

// APè´¦å·å’Œå¯†ç 
const char* ssid = "AIWifi";
const char* password = "";

// Wifié…ç½®
WebServer server(8080);
const char* configFile = "/config.json";

WebsocketsClient wsSpeech;  // ç”¨äºè¯­éŸ³è½¬æ–‡å­—
WebsocketsClient wsChat;    // ç”¨äºå¤§æ¨¡å‹å¯¹è¯
WebsocketsClient wsTTS;     // ç”¨äºttsè½¬è¯­éŸ³

// ç§‘å¤§è®¯é£ï¼ˆè¯­éŸ³è½¬æ–‡å­—ï¼‰APIç›¸å…³
const char* speechHost = "iat-api.xfyun.cn";
const char* speechPath = "/v2/iat";
// è®¯é£å¤§æ¨¡å‹
const char* chatHost = "spark-api.xf-yun.com";
const char* chatPath = "/v4.0/chat";
// è®¯é£è¯­éŸ³åˆæˆ
const char* ttsHost = "tts-api.xfyun.cn";
const char* ttsPath = "/v2/tts";

String speechText;                  //ttsè¿”å›çš„ç”¨æˆ·è¾“å…¥è¯­éŸ³
String chatAggregated;              //ç´¯è®¡å¤§æ¨¡å‹è¿”å›çš„æ–‡å­—
unsigned long lastChatMsgTime = 0;  //æœ€åå¤§æ¨¡å‹çš„æ—¶é—´ï¼Œè®¡ç®—ä¸‹é—´éš”ä»¥ååœ¨æ’­æ”¾
String lastAudioUrl;
String ttsAudioBase64;

// è®¡æ—¶å˜é‡ï¼ˆéŸ³é¢‘å‘é€ï¼‰
unsigned long startTime = 0;
unsigned long lastSendTime = 0;
// çŠ¶æ€æ ‡è®°åŠç»“æœå­˜å‚¨
volatile bool isRecording = false;     // å½“å‰æ˜¯å¦æ­£åœ¨å½•éŸ³
volatile bool speechFinished = false;  // è¯­éŸ³è¯†åˆ«ç»“æœæ˜¯å¦è¿”å›ï¼ˆç»“æŸå¸§


int16_t audioData[2560];
int16_t* pcm_data;  //å½•éŸ³ç¼“å­˜åŒº
uint recordingSize = 0;
#define I2S_PORT_0 I2S_NUM_0
#define SAMPLE_RATE 16000
#define RECORD_TIME_SECONDS 60
#define BUFFER_SIZE (SAMPLE_RATE * RECORD_TIME_SECONDS)

// æ—¶é—´ntp
WiFiUDP udp;
NTPClient timeClient(udp, "cn.pool.ntp.org", 0, 60000);


// é¦–é¡µçš„ç½‘é¡µ
const char indexHtml[] PROGMEM = R"rawliteral(
<!DOCTYPE html>
<html lang='en'>
<head>
    <meta charset='UTF-8'>
    <meta name='viewport' content='width=device-width, initial-scale=1.0'>
    <title>AI Config</title>
    <style>

        body {
            margin: 0;
            padding: 0;
            font-family: Arial, sans-serif;
        }

        .container {
            max-width: 800px;
            margin: 0 auto;
            padding: 20px;
            text-align: center;
        }

        h1 {
            text-align: center;
        }

        .button {
            display: inline-block;
            height: 30px;
            width: 300px;
            margin-top: 20px;

            padding: 10px 20px;
            background-color: deepskyblue;
            color: #fff;
            border: none;
            border-radius: 20px; /* æ·»åŠ åœ†è§’ */
            text-decoration: none;
            line-height: 2; /* é€šè¿‡è°ƒæ•´line-heightçš„å€¼æ¥è°ƒæ•´æ–‡å­—çš„å‚ç›´ä½ç½® */
            text-align: center; /* æ–‡å­—å±…ä¸­ */
            box-shadow: 2px 2px 5px rgba(0, 0, 0, 0.2); /* æ·»åŠ ç«‹ä½“æ„Ÿ */
            transition: all 0.3s ease; /* æ·»åŠ è¿‡æ¸¡æ•ˆæœ */
        }

        .button:hover {
            background-color: skyblue; /* é¼ æ ‡æ‚¬åœæ—¶çš„èƒŒæ™¯é¢œè‰² */
            transform: translateY(2px); /* ç‚¹å‡»æ•ˆæœ */
            box-shadow: 2px 2px 8px rgba(0, 0, 0, 0.3); /* æ·»åŠ æ›´å¤šç«‹ä½“æ„Ÿ */
        }
        .search-box {
            margin-top: 20px;
            display: inline-block;
            height: 30px;
            width: 300px;
            padding: 5px 10px;
            background-color: #f0f0f0;
            border: 1px solid #ccc;
            border-radius: 20px;
            text-align: center; /* æ–‡å­—å±…ä¸­ */
        }
        .hidden {
            display: none; /* åˆå§‹éšè— */
        }
    </style>

</head>
<body>
<form action='/config' method='POST'>
    <div class='container'>
        <h1>è®¾å¤‡é…ç½®é¡µ</h1>
        <input type='text' name='ssid' placeholder='è¾“å…¥WIFIåç§°' class='search-box'>
        <input type='text' name='pass' placeholder='è¾“å…¥WIFIå¯†ç ' class='search-box'>
        <input type='text' name='appid' placeholder='è¾“å…¥è®¯é£Appid' class='search-box'>
        <input type='text' name='apikey' placeholder='è¾“å…¥è®¯é£ApiKey' class='search-box'>
        <input type='text' name='apisecret' placeholder='è¾“å…¥è®¯é£ApiSecret' class='search-box'>
        <input type='submit'  style="height: 50px;width: 320px"  class='button'  value="ä¿å­˜">
    </div>
</form>
</body>
</html>
)rawliteral";


typedef struct {
  String ssid;
  String pass;
  String appid;
  String apikey;
  String apisecret;
  String ttsapikey;
} SystemConfig;
// å…¨å±€å˜é‡å£°æ˜
SystemConfig globalConfig;

// OLEDå±å¹•
U8G2_SSD1306_128X32_UNIVISION_F_HW_I2C u8g2(U8G2_R0, /* reset=*/U8X8_PIN_NONE, /* SCL=*/OLED_SCL, /* SDA=*/OLED_SDA);



// ä» SPIFFS åŠ è½½é…ç½®
bool loadConfig() {
  if (!SPIFFS.begin(true)) {
    sendMsg("", "SPIFFS åˆå§‹åŒ–å¤±è´¥");
    return false;
  }
  File file = SPIFFS.open(configFile, "r");
  if (!file) {
    sendMsg("ç³»ç»Ÿæ–‡ä»¶ä¸å­˜åœ¨", "http://192.168.4.1:8080/");
    return false;
  }
  DynamicJsonDocument doc(2048);
  DeserializationError error = deserializeJson(doc, file);
  if (error) {
    sendMsg("ç³»ç»Ÿæ–‡ä»¶æœªè®¾ç½®", "http://192.168.4.1:8080/");
    file.close();
    return false;
  }
  // è¯»å–æ•°æ®ï¼Œå¹¶ä¸”è½¬æ¢æˆ SystemConfig ç»“æ„ä½“
  globalConfig.ssid = doc["ssid"].as<String>();
  globalConfig.pass = doc["pass"].as<String>();
  globalConfig.appid = doc["appid"].as<String>();
  globalConfig.apikey = doc["apikey"].as<String>();
  globalConfig.apisecret = doc["apisecret"].as<String>();
  // ç©ºæ•°æ®çš„æ—¶å€™
  if (globalConfig.ssid == "" || globalConfig.pass == "") {
    sendMsg("è¯·å…ˆé“¾æ¥çƒ­ç‚¹é…ç½®ç³»ç»Ÿ", "http://192.168.4.1:8080/");
    file.close();
    return false;
  }
  Serial.printf("ssid=%s,pass=%s", globalConfig.ssid.c_str(), globalConfig.pass.c_str());
  Serial.printf("appid=%s,apikey=%s,apisecret=%s", globalConfig.appid.c_str(), globalConfig.apikey.c_str(), globalConfig.apisecret.c_str());
  Serial.println("");

  // å…³é—­æ–‡ä»¶
  file.close();
  return true;
}

// åˆå§‹åŒ–æŒ‰é’®
void initButton() {
  // ä½¿ç”¨å†…éƒ¨ä¸Šæ‹‰
  pinMode(BUTTON_PIN_1, INPUT_PULLUP);
  pinMode(BUTTON_PIN_2, INPUT_PULLUP);
  buttonLastState1 = HIGH;
  buttonLastState2 = HIGH;
  Serial.println("initButton finished");
}
// åˆå§‹åŒ– I2S éº¦å…‹é£
void initI2SMic() {
  i2s_config_t i2s_config = {
    .mode = static_cast<i2s_mode_t>(I2S_MODE_MASTER | I2S_MODE_RX),
    .sample_rate = SAMPLE_RATE,
    .bits_per_sample = I2S_BITS_PER_SAMPLE_16BIT,
    .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
    .communication_format = I2S_COMM_FORMAT_I2S_MSB,
    .intr_alloc_flags = ESP_INTR_FLAG_LEVEL1,
    .dma_buf_count = 4,
    .dma_buf_len = 512,
    .use_apll = false,
    .tx_desc_auto_clear = false,
    .fixed_mclk = 0
  };

  const i2s_pin_config_t inmp441_pin_config = {
    .bck_io_num = I2S_MIC_SCK,
    .ws_io_num = I2S_MIC_WS,
    .data_out_num = I2S_PIN_NO_CHANGE,
    .data_in_num = I2S_MIC_SD
  };
  esp_err_t err;
  err = i2s_driver_install(I2S_NUM_0, &i2s_config, 0, NULL);
  if (err != ESP_OK) {
    Serial.print("i2s_driver_install I2S_NUM_0 failed with error: ");
    Serial.println(err);  // æ‰“å°å‡ºæ•´æ•°é”™è¯¯ç 
  }
  err = i2s_set_pin(I2S_NUM_0, &inmp441_pin_config);
  if (err != ESP_OK) {
    Serial.print("i2s_set_pin I2S_NUM_0 failed with error: ");
    Serial.println(err);  // æ‰“å°å‡ºæ•´æ•°é”™è¯¯ç 
  }
  err = i2s_zero_dma_buffer(I2S_NUM_0);
  if (err != ESP_OK) {
    Serial.print("i2s_zero_dma_buffer I2S_NUM_0 failed with error: ");
    Serial.println(err);  // æ‰“å°å‡ºæ•´æ•°é”™è¯¯ç 
  }

  Serial.println("initI2SMic finished");
}
// åˆå§‹åŒ– I2S æ‰¬å£°å™¨
void initI2SSpeaker() {
  i2s_config_t i2s_config = {
    .mode = i2s_mode_t(I2S_MODE_MASTER | I2S_MODE_TX),
    .sample_rate = SAMPLE_RATE,
    .bits_per_sample = I2S_BITS_PER_SAMPLE_16BIT,
    .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
    .communication_format = I2S_COMM_FORMAT_I2S_MSB,
    .intr_alloc_flags = ESP_INTR_FLAG_LEVEL1,
    .dma_buf_count = 4,
    .dma_buf_len = 512,
    .use_apll = false
  };
  i2s_pin_config_t pin_config = {
    .bck_io_num = I2S_SPK_BCLK,
    .ws_io_num = I2S_SPK_LRC,
    .data_out_num = I2S_SPK_DIN,
    .data_in_num = I2S_PIN_NO_CHANGE
  };
  esp_err_t err;
  err = i2s_driver_install(I2S_NUM_1, &i2s_config, 0, NULL);
  if (err != ESP_OK) {
    Serial.print("i2s_driver_install I2S_NUM_1 failed with error: ");
    Serial.println(err);
  }
  err = i2s_set_pin(I2S_NUM_1, &pin_config);
  if (err != ESP_OK) {
    Serial.print("i2s_set_pin I2S_NUM_1 failed with error: ");
    Serial.println(err);
  }
  err = i2s_zero_dma_buffer(I2S_NUM_1);
  if (err != ESP_OK) {
    Serial.print("i2s_zero_dma_buffer I2S_NUM_1 failed with error: ");
    Serial.println(err);
  }

  Serial.println("initI2SSpeaker finished");
}
// åˆå§‹åŒ–wifi
void initWifi() {
  if (globalConfig.ssid.length() > 0 && globalConfig.pass.length() > 0) {
    WiFi.begin(globalConfig.ssid.c_str(), globalConfig.pass.c_str());
    sendMsg("æ­£åœ¨è¿æ¥WIFI", globalConfig.ssid);
    // ç­‰å¾…WiFiè¿æ¥ï¼Œæœ€å¤šç­‰å¾…10ç§’
    int attempts = 0;
    while (WiFi.status() != WL_CONNECTED && attempts < 20) {
      delay(500);
      attempts++;
    }
    if (WiFi.status() == WL_CONNECTED) {
      sendMsg("WIFIå·²è¿æ¥", "IP: " + WiFi.localIP().toString());
    } else {
      sendMsg("WIFIè¿æ¥å¤±è´¥", "è¯·æ£€æŸ¥é…ç½®");
    }
  }
  Serial.println("initWifi finished");
}

void initNtp() {
  timeClient.begin();
  timeClient.update();  // è·å–åˆå§‹æ—¶é—´
  Serial.println("initNtp finished");
}

void initServer() {
  server.on("/config", HTTP_POST, []() {
    // è·å–è¡¨å•æ•°æ®
    String ssid = server.arg("ssid");
    String pass = server.arg("pass");
    String appid = server.arg("appid");
    String apikey = server.arg("apikey");
    String apisecret = server.arg("apisecret");
    Serial.println("initServer ssid = " + ssid);
    Serial.println("initServer pass = " + pass);
    Serial.println("initServer appid = " + appid);
    Serial.println("initServer apikey = " + apikey);
    Serial.println("initServer apisecret = " + apisecret);
    // åˆ›å»ºJSONæ–‡æ¡£
    DynamicJsonDocument doc(2048);
    doc["ssid"] = ssid;
    doc["pass"] = pass;
    doc["appid"] = appid;
    doc["apikey"] = apikey;
    doc["apisecret"] = apisecret;
    // ä¿å­˜åˆ°SPIFFS
    File file = SPIFFS.open(configFile, "w");
    if (!file) {
      server.send(500, "text/plain", "Save fail");
      return;
    }
    // åºåˆ—åŒ–JSONåˆ°æ–‡ä»¶
    if (serializeJson(doc, file)) {
      server.send(200, "text/plain", "Save success, Rebot system now!");
      file.close();
      delay(1000);
      ESP.restart();
    } else {
      server.send(500, "text/plain", "ä¿å­˜å¤±è´¥");
    }
    Serial.println("config save done");
  });
  server.on("/", HTTP_GET, []() {
    String html((__FlashStringHelper*)indexHtml);
    server.send(200, "text/html", html);
    Serial.println("index loaded");
  });
  server.begin();
  Serial.println("HTTP æœåŠ¡å™¨å·²å¯åŠ¨ï¼Œç›‘å¬ç«¯å£ 8080");
  Serial.println("initServer finished");
}



void listenButtonEvent(uint8_t pin, bool& lastState, void (*onPress)(), void (*onRelease)()) {
  bool currentState = digitalRead(pin);
  // Serial.println("currentState=" + currentState);
  // æ£€æµ‹æŒ‰ä¸‹äº‹ä»¶ï¼ˆHIGH -> LOWï¼‰
  if (lastState == HIGH && currentState == LOW) {
    if (onPress) onPress();
  }
  // æ£€æµ‹é‡Šæ”¾äº‹ä»¶ï¼ˆLOW -> HIGHï¼‰
  else if (lastState == LOW && currentState == HIGH) {
    if (onRelease) onRelease();
  }
  // æ›´æ–°çŠ¶æ€
  lastState = currentState;
}


void connectToIFLY() {
  String wsUrl = generateXunFeiAuthURL(speechHost, speechPath);
  if (wsSpeech.available()) {
    wsSpeech.close();
  }
  bool connected = wsSpeech.connect(wsUrl);
  wsSpeech.onMessage([](WebsocketsMessage message) {
    Serial.println("[tts2text]è¿”å›å†…å®¹: " + message.data());
    DynamicJsonDocument doc(2048);
    DeserializationError err = deserializeJson(doc, message.data());
    if (err.c_str() != "Ok") {
      Serial.print("[tts2text]SONè§£æé”™è¯¯ï¼š");
      Serial.println(err.c_str());
      sendMsg("è¯­éŸ³è§£æé”™è¯¯", err.c_str());
      return;
    }
    //æ‹¿å‡ºæ•°æ®
    String tempText = "";
    if (doc.containsKey("data") && doc["data"].containsKey("result") && doc["data"]["result"].containsKey("ws")) {
      for (JsonObject wsObj : doc["data"]["result"]["ws"].as<JsonArray>()) {
        for (JsonObject cwObj : wsObj["cw"].as<JsonArray>()) {
          tempText += cwObj["w"].as<String>() + " ";
        }
      }
      tempText.trim();
      speechText = tempText;
      Serial.println("[tts2text]è¯†åˆ«ç»“æœ tempTextï¼š" + tempText);
      // speechText = "ä½ å¥½è‹±æ–‡æ€ä¹ˆè¯´";
      Serial.println("[tts2text]è¯†åˆ«ç»“æœ speechTextï¼š" + speechText);
      wsSpeech.close();  //å…³é—­è¿æ¥
    }
  });
  // ç­‰å¾… WebSocket è¿æ¥å»ºç«‹
  unsigned long startTime = millis();
  while (!wsSpeech.available() && millis() - startTime < 1000) {
    delay(10);
  }
  // å‘ä¸ªhi
  DynamicJsonDocument jsonDoc(2048);
  jsonDoc["common"]["app_id"] = globalConfig.appid;
  jsonDoc["business"]["language"] = "zh_cn";
  jsonDoc["business"]["domain"] = "iat";       //iatï¼šæ—¥å¸¸ç”¨è¯­
  jsonDoc["business"]["accent"] = "mandarin";  //mandarinï¼šä¸­æ–‡æ™®é€šè¯ã€å…¶ä»–è¯­ç§
  jsonDoc["business"]["vad_eos"] = 3000;
  jsonDoc["data"]["status"] = 0;
  jsonDoc["data"]["format"] = "audio/L16;rate=16000";
  jsonDoc["data"]["encoding"] = "raw";
  char buf[512];
  serializeJson(jsonDoc, buf);
  if (!connected) {
    Serial.println("[tts2text]Not Connected!");
  } else {
    Serial.println("[tts2text]è¿æ¥æˆåŠŸ");
    Serial.printf("[tts2text]first jsonBuffer %s\n", buf);
    wsSpeech.send(buf);
  }
}

void sendAudioData(bool firstFrame = false) {
  Serial.println("[tts2text]sendAudioData");
  static int16_t buffer[FRAME_SIZE];  // éŸ³é¢‘æ•°æ®ç¼“å†²åŒº
  size_t bytesRead = 0;
  static unsigned long lastSendTime = 0;

  unsigned long currentMillis = millis();
  // æ¯40mså‘é€ä¸€æ¬¡éŸ³é¢‘
  if (currentMillis - lastSendTime < 40) {
    Serial.println("[tts2text]ä¸è¶³40msä¸å‘é€æ•°æ®");
    return;  // å¦‚æœé—´éš”ä¸åˆ°40msï¼Œä¸å‘é€æ•°æ®
  }
  lastSendTime = currentMillis;  // æ›´æ–°å‘é€æ—¶é—´

  // è¯»å– I2S éŸ³é¢‘æ•°æ®
  // esp_err_t result = i2s_read(I2S_NUM_0, buffer, sizeof(buffer), &bytesRead, 100 / portTICK_PERIOD_MS);
  esp_err_t result = i2s_read(I2S_NUM_0, buffer, sizeof(buffer), &bytesRead, portMAX_DELAY);
  if (result != ESP_OK || bytesRead == 0) {
    Serial.println("[tts2text]I2S Read Failed or No Data!");
    return;
  }

  String base64Audio = base64Encode((uint8_t*)buffer, bytesRead);
  // String base64Audio = base64Encode(reinterpret_cast<const uint8_t*>(buffer), bytesRead);
  // String base64Audio = base64Encode(buffer, bytesRead);
  if (base64Audio.length() == 0) {
    Serial.println("[tts2text]Base64 Encoding Failed!");
    return;
  }

  // å‘é€ JSON æ•°æ®
  DynamicJsonDocument jsonDoc(2048);
  jsonDoc["data"]["status"] = firstFrame ? 0 : 1;  // ç¬¬ä¸€å¸§ status = 0ï¼Œå…¶ä»–å¸§ status = 1
  jsonDoc["data"]["format"] = "audio/L16;rate=16000";
  jsonDoc["data"]["encoding"] = "raw";
  jsonDoc["data"]["audio"] = base64Audio;  // ç¡®ä¿ Base64 ç¼–ç æˆåŠŸ

  String jsonBufferStr;
  serializeJson(jsonDoc, jsonBufferStr);

  // String jsonStr;
  // serializeJson(jsonDoc, jsonStr);
  Serial.printf("[tts2text]jsonBufferStr %s\n", jsonBufferStr.c_str());


  if (!wsSpeech.send(jsonBufferStr)) {
    Serial.println("[tts2text]æ•°æ®å‘é€å¤±è´¥");
  } else {
    Serial.println("[tts2text]æ•°æ®å‘é€æˆåŠŸ");
  }
}

void sendTTSRequest(const String& text) {
  Serial.println("[TTS]sendTTSRequest text:" + text);
  sendMsg("", text);

  DynamicJsonDocument doc(2048);
  JsonObject common = doc.createNestedObject("common");
  common["app_id"] = globalConfig.appid;
  JsonObject business = doc.createNestedObject("business");
  business["aue"] = "raw";
  business["vcn"] = "xiaoyan";
  business["pitch"] = 50;
  business["speed"] = 50;
  JsonObject data = doc.createNestedObject("data");
  data["status"] = 2;

  // æ­£ç¡®çš„ base64 ç¼–ç  UTF-8 æ–‡æœ¬
  const char* utf8Text = text.c_str();
  size_t utf8Len = strlen(utf8Text);

  size_t b64Len = 0;
  mbedtls_base64_encode(NULL, 0, &b64Len, (const unsigned char*)utf8Text, utf8Len);
  unsigned char* b64 = (unsigned char*)malloc(b64Len + 1);
  memset(b64, 0, b64Len + 1);

  int ret = mbedtls_base64_encode(b64, b64Len, &b64Len, (const unsigned char*)utf8Text, utf8Len);
  if (ret == 0) {
    data["text"] = String((char*)b64);
  } else {
    Serial.println("[TTS] base64ç¼–ç å¤±è´¥");
  }
  free(b64);

  // data["text"] = ;
  String output;
  serializeJson(doc, output);
  Serial.println("[TTS]æ•°æ®å‘é€,output:" + output);
  if (!wsTTS.send(output)) {
    Serial.println("[TTS]æ•°æ®å‘é€å¤±è´¥");
  } else {
    Serial.println("[TTS]æ•°æ®å‘é€æˆåŠŸ");
  }
}

void playAudio(String base64Str) {
  Serial.println("[audio]playAudio");

  // 1. è®¡ç®—è§£ç åéœ€è¦çš„æœ€å¤§é•¿åº¦
  int inputLen = base64Str.length();
  int outputMaxLen = inputLen * 3 / 4;

  // 2. ä¸º PCM æ•°æ®åˆ†é…å†…å­˜
  uint8_t* decodedAudio = (uint8_t*)malloc(outputMaxLen);
  if (!decodedAudio) {
    Serial.println("âŒ éŸ³é¢‘å†…å­˜åˆ†é…å¤±è´¥");
    return;
  }

  // 3. å°† String è½¬ä¸º C å­—ç¬¦ä¸²
  const char* base64_cstr = base64Str.c_str();

  // 4. è§£ç  Base64ï¼ˆä½¿ç”¨ä½ å‰é¢æä¾›çš„å‡½æ•°ï¼‰
  int actualLen = decode_base64(base64_cstr, decodedAudio);
  Serial.printf("âœ… è§£ç éŸ³é¢‘å­—èŠ‚æ•°: %d\n", actualLen);

  // 5. å†™å…¥ I2S æ’­æ”¾
  size_t bytes_written = 0;
  i2s_write(I2S_NUM_1, decodedAudio, actualLen, &bytes_written, 100 / portTICK_PERIOD_MS);
  Serial.printf("ğŸ”Š æ’­æ”¾å®Œæˆï¼ŒI2Så†™å…¥å­—èŠ‚æ•°: %d\n", bytes_written);

  // 6. é‡Šæ”¾å†…å­˜
  free(decodedAudio);

  // æ‰€æœ‰æ•°æ®æ’­æ”¾å®Œæˆåå†æ¸…ç©º / åœæ­¢
  i2s_zero_dma_buffer(I2S_NUM_1);
  i2s_stop(I2S_NUM_1);
}

void txt2TTS(String text) {
  if (!wsTTS.available()) {
    String ttsURL = generateXunFeiAuthURL(ttsHost, ttsPath);
    Serial.println("[TTS]è¿æ¥ URLï¼š" + ttsURL);
    wsTTS.onMessage([](WebsocketsMessage message) {
      Serial.println("[TTS]è¿”å›å†…å®¹: " + message.data());
      DynamicJsonDocument doc(4096);
      DeserializationError err = deserializeJson(doc, message.data());
      if (err.c_str() != "Ok") {
        Serial.print("[TTS]å¤§æ¨¡å‹JSONè§£æé”™è¯¯:");
        Serial.println(err.c_str());
        return;
      }
      int code = doc["code"];
      if (code == 0) {
        // æå–å½“å‰å›å¤å†…å®¹å’Œåºå·
        if (doc["data"].isNull()) {
          return;
        }
        int status = doc["data"]["status"];
        ttsAudioBase64 = doc["data"]["audio"].as<String>();
        playAudio(ttsAudioBase64);
        if (status == 2) {
          wsTTS.close();  //å…³é—­è¿æ¥
        }
      } else {
        Serial.println("[TTS]è¯·æ±‚å¤±è´¥ï¼Œé”™è¯¯ç ï¼š" + String(code));
      }
    });
    bool connected = wsTTS.connect(ttsURL);
    unsigned long startTime = millis();
    while (!wsTTS.available() && millis() - startTime < 1000) {
      delay(10);
    }
    if (!connected) {
      Serial.println("[TTS]Not Connected!");
    } else {
      Serial.println("[TTS]è¿æ¥æˆåŠŸ");
      sendTTSRequest(text);
    }
  } else {
    sendTTSRequest(text);
  }
}

void sendChatRequest(const String& userInput) {
  DynamicJsonDocument doc(2048);
  JsonObject header = doc.createNestedObject("header");
  header["app_id"] = globalConfig.appid;
  JsonObject parameter = doc.createNestedObject("parameter");
  JsonObject chat = parameter.createNestedObject("chat");
  chat["domain"] = "4.0Ultra";
  chat["temperature"] = 0.5;
  chat["max_tokens"] = 1024;
  JsonObject payload = doc.createNestedObject("payload");
  JsonObject message = payload.createNestedObject("message");
  JsonArray textArr = message.createNestedArray("text");
  // ç³»ç»Ÿæç¤ºï¼ˆå¯æ ¹æ®éœ€è¦ä¿®æ”¹ï¼‰
  JsonObject systemMsg = textArr.createNestedObject();
  systemMsg["role"] = "system";
  systemMsg["content"] = "ä½ æ˜¯ä¸ªç¿»è¯‘å‘˜,ç»™ä¸€ä¸ªå•è¯æˆ–è€…çŸ­å¥,ä½ è¿”å›å¯¹åº”çš„è‹±è¯­";
  // ç”¨æˆ·è¾“å…¥ï¼Œä½¿ç”¨è¯­éŸ³è¯†åˆ«çš„ç»“æœ
  JsonObject userMsg = textArr.createNestedObject();
  userMsg["role"] = "user";
  userMsg["content"] = userInput;
  String output;
  serializeJson(doc, output);
  Serial.println("[chat]å‘é€å†…å®¹ output:" + output);
  if (!wsChat.send(output)) {
    Serial.println("[chat]æ•°æ®å‘é€å¤±è´¥");
  } else {
    Serial.println("[chat]æ•°æ®å‘é€æˆåŠŸ");
  }
}

void processChatResult() {
}

void processSpeechResult() {
  // æ²¡æœ‰å½•éŸ³ï¼Œå¹¶ä¸”æœ‰ç»“æœçš„æ—¶å€™å¤„ç†é€»è¾‘
  if (speechText == "" || isRecording) {
    return;
  }
  Serial.println("[chat]speechText:" + speechText);
  if (!wsChat.available()) {
    String chatURL = generateXunFeiAuthURL(chatHost, chatPath);
    Serial.println("[chat]å¤§æ¨¡å‹å¯¹è¯WS URL:" + chatURL);
    wsChat.onMessage([](WebsocketsMessage message) {
      Serial.println("[chat]å¤§æ¨¡å‹è¿”å›å†…å®¹:" + message.data());
      DynamicJsonDocument doc(2048);
      DeserializationError err = deserializeJson(doc, message.data());
      if (err.c_str() != "Ok") {
        Serial.print("[chat]å¤§æ¨¡å‹JSONè§£æé”™è¯¯:");
        Serial.println(err.c_str());
        return;
      }
      int code = doc["header"]["code"];
      int status = doc["header"]["status"];
      if (code == 0) {
        String content = doc["payload"]["choices"]["text"][0]["content"].as<String>();
        chatAggregated += content;
      } else {
        Serial.println("[chat]è¯·æ±‚å¤±è´¥ï¼Œé”™è¯¯ç ï¼š" + String(code));
      }
      if (status == 2) {
        Serial.println("[chat]å½“å‰ç´¯è®¡å›å¤ï¼š" + chatAggregated);
        wsChat.close();  //å…³é—­è¿æ¥
        txt2TTS(chatAggregated);
      }
    });
    bool connected = wsChat.connect(chatURL);
    unsigned long startTime = millis();
    while (!wsChat.available() && millis() - startTime < 1000) {
      delay(10);
    }
    if (!connected) {
      Serial.println("[chat]Not Connected!");
    } else {
      Serial.println("[chat]è¿æ¥æˆåŠŸ");
      sendChatRequest(speechText);
    }
  } else {
    sendChatRequest(speechText);
  }
  speechText = "";
}

void startRecording() {
  Serial.println("[btn]startRecording");

  isRecording = true;
  startTime = millis();
  sendAudioData(false);
}


void stopRecording() {
  Serial.println("[btn]stopRecording");

  // å‘ä¸ªbye
  DynamicJsonDocument jsonDoc(2048);
  jsonDoc["data"]["status"] = 2;  // ç»“æŸä¼ è¾“
  char buf[128];
  serializeJson(jsonDoc, buf);
  Serial.printf("[tts2text]jsonBuffer %s\n", buf);

  if (!wsSpeech.send(buf)) {  // å¤±è´¥é€»è¾‘
    Serial.println("[tts2text]å‘é€è¯­éŸ³å¤±è´¥,ç»“æŸæŒ‡ä»¤");
  } else {
    Serial.println("[tts2text]å‘é€è¯­éŸ³æˆåŠŸ,ç»“æŸæŒ‡ä»¤");
  }
  isRecording = false;
}

void handlePress1() {
  sendMsg("", "[btn]å¼€å§‹è¯­éŸ³è¯†åˆ«");
  connectToIFLY();
  if (!isRecording) {
    startRecording();
  }
}

void handleRelease1() {
  if (isRecording) {
    stopRecording();
  }
  sendMsg("", "[btn]æŒ‰é’®1æ¾å¼€");
}


void handlePress2() {
  sendMsg("", "[btn]é‡æ’­");
}

void handleRelease2() {
  sendMsg("", "[btn]æŒ‰é’®2æ¾å¼€");
}

void initButtonListener() {
  listenButtonEvent(BUTTON_PIN_1, buttonLastState1, handlePress1, handleRelease1);
  listenButtonEvent(BUTTON_PIN_2, buttonLastState2, handlePress2, handleRelease2);
}

void displayTask(void* parameter) {
  // ä½ çš„ä»»åŠ¡é€»è¾‘
  for (;;) {
    timeClient.update();
    // Serial.println("[task]displayTask");
    vTaskDelay(1000 / portTICK_PERIOD_MS);
  }
}

void setup() {
  Serial.begin(115200);
  while (!Serial) delay(10);  // ç­‰å¾… USB CDC å°±ç»ªï¼ˆESP32-S3 ç‰¹æœ‰ï¼‰


  delay(1000);  // ç­‰å¾…ä¸²å£ç¨³å®š
  WiFi.mode(WIFI_OFF);
  delay(100);
  WiFi.mode(WIFI_AP);

  u8g2.begin();
  u8g2.enableUTF8Print();  // å¯ç”¨ UTF-8 æ”¯æŒ
  delay(100);

  sendMsg("ç³»ç»Ÿ", "å¯åŠ¨ä¸­");
  // è®¾ç½®wifiçƒ­ç‚¹
  bool result = WiFi.softAP(ssid);
  if (result) {
    sendMsg("", "çƒ­ç‚¹åˆ›å»ºæˆåŠŸ");
  } else {
    sendMsg("", "çƒ­ç‚¹åˆ›å»ºå¤±è´¥");
  }

  initButton();
  initI2SMic();
  initI2SSpeaker();
  initServer();
  // 1ç§’åˆ·æ–°ä¸‹çš„ä»»åŠ¡ã€‚å’Œloopä¸ä¸€æ ·
  xTaskCreatePinnedToCore(displayTask, "DisplayTask", 10000, NULL, 1, NULL, 1);
  // è¯»å–é…ç½®
  bool status = loadConfig();
  if (status) {
    initWifi();
    initNtp();
    Serial.println("ç³»ç»Ÿå¯åŠ¨");
  } else {
    Serial.println("ç³»ç»Ÿå°šæœªå®Œæˆ");
  }
  // txt2TTS("How are youï¼Ÿ");
}

void loop() {
  server.handleClient();
  initButtonListener();
  wsSpeech.poll();  //æŒç»­æ¶ˆæ¯æ¥å—
  wsChat.poll();
  wsTTS.poll();

  // æŒç»­å‘é€éŸ³é¢‘æ•°æ®
  if (isRecording) {
    sendAudioData(false);
  }

  processSpeechResult();
  processChatResult();
}

String lastMsg1 = "";
String lastMsg2 = "";

void sendMsg(String msg1, String msg2) {
  u8g2.clearBuffer();
  u8g2.setFont(u8g2_font_wqy12_t_gb2312);

  // æ¸…é™¤æ¯è¡Œå†…å®¹ï¼ˆé¿å…æ®‹å½±ï¼‰
  u8g2.setDrawColor(0);
  u8g2.drawBox(0, 0, u8g2.getDisplayWidth(), 16);   // æ¸…ç¬¬1è¡Œ
  u8g2.drawBox(0, 20, u8g2.getDisplayWidth(), 16);  // æ¸…ç¬¬2è¡Œ
  u8g2.setDrawColor(1);

  // æ˜¾ç¤ºç¬¬1è¡Œ
  msg1 = msg1.length() > 0 ? msg1 : lastMsg1;
  u8g2.setCursor(0, 14);
  u8g2.print(msg1);
  lastMsg1 = msg1;

  // æ˜¾ç¤ºç¬¬2è¡Œ
  msg2 = msg2.length() > 0 ? msg2 : lastMsg2;
  u8g2.setCursor(0, 30);
  u8g2.print(msg2);
  lastMsg2 = msg2;

  u8g2.sendBuffer();
}


String generateXunFeiAuthURL(String host, String path) {
  String date = getDate();
  if (date == "")
    return "";
  String tmp = "host: " + String(host) + "\n";
  tmp += "date: " + date + "\n";
  tmp += "GET " + String(path) + " HTTP/1.1";
  String signature = hmacSHA256(globalConfig.apisecret.c_str(), tmp);
  String authOrigin = "api_key=\"" + String(globalConfig.apikey.c_str()) + "\", algorithm=\"hmac-sha256\", headers=\"host date request-line\", signature=\"" + signature + "\"";
  unsigned char authBase64[256] = { 0 };
  size_t authLen = 0;
  int ret = mbedtls_base64_encode(authBase64, sizeof(authBase64) - 1, &authLen, (const unsigned char*)authOrigin.c_str(), authOrigin.length());
  if (ret != 0)
    return "";
  String authorization = String((char*)authBase64);
  String encodedDate = "";
  for (int i = 0; i < date.length(); i++) {
    char c = date.charAt(i);
    if (isalnum(c) || c == '-' || c == '_' || c == '.' || c == '~') {
      encodedDate += c;
    } else if (c == ' ') {
      encodedDate += "+";
    } else if (c == ',') {
      encodedDate += "%2C";
    } else if (c == ':') {
      encodedDate += "%3A";
    } else {
      encodedDate += "%" + String(c, HEX);
    }
  }
  String url = "ws://" + String(host) + String(path) + "?authorization=" + authorization + "&date=" + encodedDate + "&host=" + host;
  return url;
}

////////////////////////////
// å·¥å…·å‡½æ•°ï¼šBase64ã€HMACã€æ—¶é—´æ ¼å¼è½¬æ¢ç­‰
////////////////////////////
String getDate() {
  time_t epochTime = timeClient.getEpochTime();
  struct tm* ptm = gmtime(&epochTime);  // è½¬æ¢ä¸º GMT æ—¶é—´
  char timeString[40];
  strftime(timeString, sizeof(timeString), "%a, %d %b %Y %H:%M:%S GMT", ptm);
  Serial.println("[ntp]timeString:" + String(timeString));
  return String(timeString);
}


String base64Encode(const void* data, size_t len) {
  if (!data || len == 0) return "";

  size_t outputLen = 0;
  size_t bufSize = ((len + 2) / 3) * 4 + 1;
  char* buf = (char*)malloc(bufSize);
  if (!buf) return "";

  int ret = mbedtls_base64_encode((unsigned char*)buf, bufSize, &outputLen, (const unsigned char*)data, len);
  if (ret != 0) {
    Serial.printf("Base64 ç¼–ç å¤±è´¥ï¼Œé”™è¯¯ç ï¼š%d\n", ret);
    free(buf);
    return "";
  }
  String result(buf);
  free(buf);
  return result;
}
// String base64Encode(const uint8_t* data, size_t len) {
//   if (len == 0 || data == nullptr) {
//     Serial.println("Base64ç¼–ç é”™è¯¯ï¼šæ— æ•°æ®");
//     return "";
//   }
//   size_t outputLen = 0;
//   size_t bufSize = ((len + 2) / 3) * 4 + 1;
//   char* buf = (char*)malloc(bufSize);
//   if (!buf)
//     return "";
//   int ret = mbedtls_base64_encode((unsigned char*)buf, bufSize, &outputLen, data, len);
//   if (ret != 0) {
//     free(buf);
//     return "";
//   }
//   String encoded = String(buf);
//   free(buf);
//   return encoded;
// }

String hmacSHA256(const String& key, const String& data) {
  unsigned char hmacResult[32];
  mbedtls_md_context_t ctx;
  mbedtls_md_init(&ctx);
  mbedtls_md_setup(&ctx, mbedtls_md_info_from_type(MBEDTLS_MD_SHA256), 1);
  mbedtls_md_hmac_starts(&ctx, (const unsigned char*)key.c_str(), key.length());
  mbedtls_md_hmac_update(&ctx, (const unsigned char*)data.c_str(), data.length());
  mbedtls_md_hmac_finish(&ctx, hmacResult);
  mbedtls_md_free(&ctx);
  size_t outLen;
  unsigned char base64Result[64];
  mbedtls_base64_encode(base64Result, sizeof(base64Result), &outLen, hmacResult, sizeof(hmacResult));
  return String((char*)base64Result);
}

const char base64_chars[] = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";

String base64_encode(uint8_t const* buf, unsigned int bufLen) {
  String result;
  int i = 0;
  uint8_t array3[3];
  uint8_t array4[4];

  while (bufLen--) {
    array3[i++] = *(buf++);
    if (i == 3) {
      array4[0] = (array3[0] & 0xfc) >> 2;
      array4[1] = ((array3[0] & 0x03) << 4) + ((array3[1] & 0xf0) >> 4);
      array4[2] = ((array3[1] & 0x0f) << 2) + ((array3[2] & 0xc0) >> 6);
      array4[3] = array3[2] & 0x3f;

      for (i = 0; i < 4; i++) {
        result += base64_chars[array4[i]];
      }
      i = 0;
    }
  }

  if (i) {
    for (int j = i; j < 3; j++) array3[j] = '\0';

    array4[0] = (array3[0] & 0xfc) >> 2;
    array4[1] = ((array3[0] & 0x03) << 4) + ((array3[1] & 0xf0) >> 4);
    array4[2] = ((array3[1] & 0x0f) << 2) + ((array3[2] & 0xc0) >> 6);
    array4[3] = array3[2] & 0x3f;

    for (int j = 0; j < i + 1; j++) result += base64_chars[array4[j]];
    while ((i++ < 3)) result += '=';
  }

  return result;
}


int base64CharToValue(char c) {
  if (c >= 'A' && c <= 'Z') return c - 'A';
  if (c >= 'a' && c <= 'z') return c - 'a' + 26;
  if (c >= '0' && c <= '9') return c - '0' + 52;
  if (c == '+') return 62;
  if (c == '/') return 63;
  return 0;
}

int decode_base64(const char* input, uint8_t* output) {
  int len = strlen(input);
  int i = 0, j = 0;
  uint32_t buf = 0;
  int valb = -8;

  while (i < len) {
    char c = input[i++];
    if (c == '=') break;
    int val = base64CharToValue(c);
    buf = (buf << 6) | val;
    valb += 6;
    if (valb >= 0) {
      output[j++] = (buf >> valb) & 0xFF;
      valb -= 8;
    }
  }
  return j;  // å®é™…è§£ç çš„å­—èŠ‚æ•°
}
